class Publication
types
	public String = SISP`String;
instance variables

	private title: String;
	private release_date: SISP`Date;
	private authors: set of Author;
	private citations: set of Publication; --publications this publication citates
	private citated: set of Publication; --publications that citate this publication
	private affiliations:	map Author to String;

operations
	public Publication: String * SISP`Date * set of Author * set of Publication * map Author to String==> Publication
		Publication(name,date,aut,cits,aff) ==
		(
			title:=name;
			release_date := date;
			authors:= aut;
			citations:=cits;
			citated:={};
			affiliations:=aff;
			for all c in set citations do
			(
				c.addCitated(self);
			);
			for all a in set authors do
			(
				a.addPublication(self);
			);
		)
		pre (dom aff = aut) and (forall a in set cits  & (date.year > a.getDate().year) or (date.year = a.getDate().year and date.month > a.getDate().month) or  (date.year = a.getDate().year and date.month = a.getDate().month and date.day > a.getDate().day));
	
	public pure getDate: () ==> SISP`Date
		getDate() ==
			return release_date;
	
	public pure getAuthors : () ==> set of Author
		getAuthors() ==
			return authors;		
	
	public pure getCitations : () ==> set of Publication
		getCitations() ==
			return citations;
		
	public pure getCitated : () ==> set of Publication
		getCitated() ==
			return citated;
			
	public addCitated : Publication ==> ()
		addCitated(p) ==
			citated := citated union {p}
		pre p not in set citated
		post p in set citated;
	
		
end Publication